plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
//	id 'org.flywaydb.flyway' version '9.4.0' // enables use from command line
	id 'war' // extends the Java plugin
}

group = 'com.github.kafousis'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

sourceSets {
	main {
		resources {
			srcDir '../frontend/build/dist'
		}
	}
}

processResources.dependsOn ':frontend:npmBuild'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-ui:1.6.11'
	implementation 'org.springdoc:springdoc-openapi-data-rest:1.6.11'
	implementation 'org.springdoc:springdoc-openapi-security:1.6.11'
	implementation 'org.flywaydb:flyway-core:9.4.0' // migrate on application startup
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
//	runtimeOnly 'org.postgresql:postgresql'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

bootWar {
	// result WAR file
	destinationDirectory = rootProject.file('build/')
	archiveFileName = 'spring-vuejs-auth.war'
}

tasks.named('test') {
	useJUnitPlatform()
}

//flyway {
//	url = 'jdbc:h2:mem:testdb'
//	user = 'sa'
//	password = ''
//	defaultSchema = 'public'
//}
